apply plugin: "groovy"

ext {
    // The drivers we want to use
    drivers = ["chrome", "chromeHeadless"]
}

repositories {
    mavenCentral()
}

dependencies {
    def groovyVersion = "2.4.12"
    def gebVersion = "2.1"
    def seleniumVersion = "3.6.0"

    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude group: "org.codehaus.groovy"
    }

    // Need a driver implementation
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
}

drivers.each { driver -> task "${driver}Test"(type: Test) {
        group JavaBasePlugin.VERIFICATION_GROUP

        outputs.upToDateWhen { false }  // Always run tests

        systemProperty "geb.build.reportsDir", reporting.file("geb/$name")
        systemProperty "geb.env", driver
    }
}

test {
    enabled = false
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
    }
}